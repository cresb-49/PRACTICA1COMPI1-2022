package com.carlos.pruebas.lexerParser;

import com.carlos.pruebas.ED.Pila;
import com.carlos.pruebas.obj.Token;
import com.carlos.pruebas.obj.Union;

import com.carlos.pruebas.obj.GraficaBarra;
import com.carlos.pruebas.obj.GraficaPie;

import java_cup.runtime.*;

class ParserGraphics;

parser code {:
    // Connect this parser to a scanner!
    private Lexer lexer;
    
    public ParserGraphics (Lexer lexer){ 
        super(lexer);
        this.lexer=lexer;
    }

    public void report_error(String message, Object info) {
        System.out.println("public void report_error");
    }
    
    public void report_fatal_error(String message, Object info) {
        System.out.println("public void report_fatal_error");
    }

    public void syntax_error(Symbol cur_token) {
        System.out.println("El error es el simbolo: " + ParserGraphicsSym.terminalNames[cur_token.sym]);
        System.out.println(String.format("En la posicion: %d, %d", cur_token.left, cur_token.right));
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
        if (cur_token.sym == ParserGraphicsSym.EOF) {
            System.out.println("public void unrecoveredsyntax_error");
        }
    }

    protected int error_sync_size() {
		return 1;
	}
:}

/* define how to connect to the scanner! */
scan with {: return this.lexer.next_token(); :};

/* Terminals (tokens returned by the scanner). */
terminal            DEF, GRAPHICBARRA,GRAPHICPIE, TITTLE, EJEX, EJEY, LABEL, VALUES,
                    LINK,TYPE,TYPEVALUE,TOTAL,EXTRA,EXECUTE,MAS,MENOS,MUL,DIV,PA_A,
                    PA_C,CO_A,CO_C,LLA_A,LLA_C,COMA,DOSPUNTOS,PUNTOCOMA,STRING,NUMBERS,
                    DECIMAL;


/* Non terminals */
non terminal                s;
non terminal GraficaPie     contPie;
non terminal GraficaBarra   contBarra;
non terminal Pila<Double>   contEjeY;
non terminal Pila<String>   contEjeX;
non terminal Pila<Union>    contUnir;   
non terminal Double         e,t,f;


/* The grammar rules */

s   ::= s DEF GRAPHICBARRA contBarra:gb LLA_C   {:
                                                    if(gb!=null){
                                                        System.out.println("Grafica: "+gb.toString());
                                                    }
                                                :}
    |   s DEF GRAPHICPIE contPie:gp LLA_C   {:
                                                if(gp!=null){
                                                    System.out.println("Grafica: "+gp.toString());
                                                }
                                            :}
    |   s EXECUTE PA_A STRING:gn PA_C PUNTOCOMA {:
                                                    System.out.println("Grafico ejecutar: "+(String)((Token)gn).getValue());
                                                :}
    |   DEF GRAPHICBARRA contBarra:gb LLA_C {:
                                                if(gb!=null){
                                                    System.out.println("Grafica: "+gb.toString());
                                                }
                                            :}
    |   DEF GRAPHICPIE contPie:gp LLA_C {:
                                            if(gp!=null){
                                                System.out.println("Grafica: "+gp.toString());
                                            }
                                        :}
    ;

//CONTENIDO DE UNA GRAFICA DE BARRAS

contBarra   ::= contBarra:gb TITTLE DOSPUNTOS STRING:titulo PUNTOCOMA  {:
                                                                            String tittle = (String)((Token)titulo).getValue();
                                                                            //System.out.println("Titulo de grafico: "+tittle);
                                                                            if(gb !=null){
                                                                                RESULT = gb;
                                                                                RESULT.setTitulo(tittle);
                                                                            }

                                                                        :}
            |   contBarra:gb EJEX DOSPUNTOS CO_A contEjeX:pila PUNTOCOMA   {:
                                                                                if(pila!=null){
                                                                                    //System.out.println("Datos del eje x");
                                                                                    //pila.imprimirPila();
                                                                                    if(gb !=null){
                                                                                        RESULT = gb;
                                                                                        //RESULT.setEjex();
                                                                                    }
                                                                                }
                                                                            :}
            |   contBarra:gb EJEY DOSPUNTOS CO_A contEjeY:pila PUNTOCOMA    {:
                                                                                if(pila!=null){
                                                                                    //System.out.println("Datos del eje y");
                                                                                    //pila.imprimirPila();
                                                                                    if(gb !=null){
                                                                                        RESULT = gb;
                                                                                        //RESULT.setEjey();
                                                                                    }
                                                                                }
                                                                            :}
            |   contBarra:gb LINK DOSPUNTOS CO_A contUnir:pila PUNTOCOMA    {:
                                                                                if(pila!=null){
                                                                                    //System.out.println("Datos de union");
                                                                                    //pila.imprimirPila();
                                                                                    if(gb !=null){
                                                                                        RESULT = gb;
                                                                                        RESULT.setUnir(pila.toArrayList());
                                                                                    }
                                                                                }
                                                                            :}
            |   LLA_A                                                       {:
                                                                                RESULT = new GraficaBarra();
                                                                            :}
            |   contBarra error
            ;

contPie     ::= contPie:gp TITTLE DOSPUNTOS STRING:titulo PUNTOCOMA {:
                                                                        if(gp!=null){
                                                                            RESULT = gp;
                                                                            RESULT.setTitulo((String)((Token)titulo).getValue());
                                                                        }
                                                                    :}
            |   contPie:gp TYPE DOSPUNTOS TYPEVALUE:tipo PUNTOCOMA  {:
                                                                        if(gp!=null){
                                                                            RESULT = gp;
                                                                            RESULT.setTipo((String)((Token)tipo).getValue());
                                                                        }
                                                                    :}
            |   contPie:gp LABEL DOSPUNTOS CO_A contEjeX:pila PUNTOCOMA {:
                                                                            if(pila!=null){
                                                                                if(gp!=null){
                                                                                    RESULT = gp;
                                                                                    //RESULT.setEtiquetas();
                                                                                }
                                                                            }
                                                                        :}
            |   contPie:gp VALUES DOSPUNTOS CO_A contEjeY:pila PUNTOCOMA    {:
                                                                                if(pila!=null){
                                                                                    if(gp!=null){
                                                                                        RESULT = gp;
                                                                                        //RESULT.setValores();
                                                                                    }
                                                                                }
                                                                            :} 
            |   contPie:gp TOTAL DOSPUNTOS e:num PUNTOCOMA  {:
                                                                if(gp!=null){
                                                                    RESULT = gp;
                                                                    if(num!=null){
                                                                        RESULT.setTotal(num);
                                                                    }
                                                                }
                                                            :}
            |   contPie:gp LINK DOSPUNTOS CO_A contUnir:pila PUNTOCOMA  {:
                                                                            if(pila!=null){
                                                                                if(gp!=null){
                                                                                    RESULT = gp;
                                                                                    RESULT.setUnir(pila.toArrayList());
                                                                                }
                                                                            }
                                                                        :}
            |   contPie:gp EXTRA DOSPUNTOS STRING:extra PUNTOCOMA   {:
                                                                        if(gp!=null){
                                                                            RESULT = gp;
                                                                            RESULT.setExtra((String)((Token)extra).getValue());
                                                                        }
                                                                    :}
            |   LLA_A   {:
                            RESULT = new GraficaPie();
                        :}
            |   contPie error
            ;


//GRAMAICA PARA EL RECONOCIMIENTO DE NUMEROS ENTEROS Y DECIMALES
e   ::= e:num1 MAS t:num2   {:
                                RESULT = (num1 + num2);
                            :}
    |   e:num1 MENOS t:num2 {:
                                RESULT = (num1 - num2);
                            :}
    |   t:exp               {:
                                RESULT = exp;
                            :}
    ;

t   ::= t:num1 MUL f:num2   {:
                                RESULT = (num1*num2);
                            :}
    |   t:num1 DIV f:num2   {:
                                RESULT = (num1/num2);
                            :}             
    |   f:exp               {:
                                RESULT = exp;
                            :}
    ;

f   ::= NUMBERS:num     {:
                            RESULT = (Double) ((Token)num).getValue();
                        :}
    |   DECIMAL:num     {:
                            RESULT = (Double) ((Token)num).getValue();
                        :}
    |   PA_A e:exp PA_C
                        {:
                            RESULT = exp;
                        :}
    |   error f
    ;
// recuperacion de vectores del lenguaje
contEjeY    ::= e:num1 COMA contEjeY:pila   {:
                                                RESULT = pila;
                                                if(RESULT != null){
                                                    RESULT.push(num1);
                                                }
                                            :}
            |   e:num1 CO_C {:
                                RESULT = new Pila<>();
                                RESULT.push(num1);
                            :}
            ;

contEjeX    ::= STRING:cadena COMA contEjeX:pila    {:
                                                        RESULT = pila;
                                                        if(RESULT!=null){
                                                            String strin = (String)((Token)cadena).getValue();
                                                            RESULT.push(strin);                    
                                                        }
                                                    :}
            |   STRING:cadena CO_C  {:
                                        RESULT = new Pila<>();
                                        String strin = (String)((Token)cadena).getValue();
                                        RESULT.push(strin);
                                    :}
            ;

contUnir    ::= LLA_A e:num1 COMA e:num2 LLA_C COMA contUnir:pila   {:
                                                                        RESULT = pila;
                                                                        if(RESULT!=null){
                                                                            RESULT.push(new Union(num1.intValue(),num2.intValue()));    
                                                                        }
                                                                    :}
            |   LLA_A e:num1 COMA e:num2 LLA_C CO_C                 {:
                                                                        RESULT = new Pila<>();
                                                                        RESULT.push(new Union(num1.intValue(),num2.intValue()));
                                                                    :}
            ;