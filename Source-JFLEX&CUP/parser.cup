package com.carlos.pruebas.lexerParser;

import java.util.ArrayList;

import com.carlos.pruebas.obj.ErrorAnalisis;
import com.carlos.pruebas.ED.Pila;
import com.carlos.pruebas.obj.Token;
import com.carlos.pruebas.obj.Union;

import com.carlos.pruebas.obj.GraficaBarra;
import com.carlos.pruebas.obj.GraficaPie;

import java_cup.runtime.*;

class ParserGraphics;

parser code {:
    // Connect this parser to a scanner!

    private static final String ERROR_TYPE_SIN = "Sintactico";
    private static final String ERROR_TYPE_SEM = "Semantico";

    private Lexer lexer;
    private SimbolosTerminales simbolosTerminales;


    
    public ParserGraphics (Lexer lexer){ 
        super(lexer);
        this.lexer=lexer;
        this.simbolosTerminales = new SimbolosTerminales();
    }

    public void report_error(String message, Object info) {
        System.out.println("public void report_error");
    }
    
    public void report_fatal_error(String message, Object info) {
        System.out.println("public void report_fatal_error");
    }

    public void syntax_error(Symbol cur_token) {
        Token tok = (Token) cur_token.value;
        String er = "Simbolo inesperado, se esperaba: "+ simbolosTerminales.obtenerSimbolos(expected_token_ids()).toString();
        this.lexer.getErrors().push(new ErrorAnalisis(ERROR_TYPE_SIN,tok.getLexema(), tok.getLinea(), tok.getColumna(), er));
        System.out.println(er);
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
        if (cur_token.sym == ParserGraphicsSym.EOF) {
            String er = "Error irrecuperable se llego al final del archivo";
            this.lexer.getErrors().push(new ErrorAnalisis(ERROR_TYPE_SIN, "", cur_token.left, cur_token.right, er));
            System.out.println(er);
        } else {
            Token tok = (Token) cur_token.value;
            String er = "Error irrecuperable, un posible simbolo esperado: "+ simbolosTerminales.obtenerSimbolos(expected_token_ids()).toString();
            this.lexer.getErrors().push(new ErrorAnalisis(ERROR_TYPE_SIN, "", tok.getLinea(), tok.getColumna(), er));
            System.out.println(er);
        }
    }

    public void semantic_error(Token token,String contexto) {
        this.lexer.getErrors().push(new ErrorAnalisis(ERROR_TYPE_SEM,token.getLexema(), token.getLinea(), token.getColumna(), contexto));
    }

    public boolean definition_error(Token ini,Token fin,ArrayList<String> errores){
        boolean status = false;
        for (String errore : errores) {
            status = true;
            String error = "Error en definicion, "+errore+"\nUbicacion general --> Linea: "+ini.getLinea()+" a Linea: "+fin.getLinea();
            this.lexer.getErrors().push(new ErrorAnalisis(ERROR_TYPE_SEM, "", ini.getLinea(), ini.getColumna(), error));
            System.out.println(error);
        }
        return status;
    }

    protected int error_sync_size() {
		return 1;
	}
:}

/* define how to connect to the scanner! */
scan with {: return this.lexer.next_token(); :};

/* Terminals (tokens returned by the scanner). */
terminal            DEF, GRAPHICBARRA,GRAPHICPIE, TITTLE, EJEX, EJEY, LABEL, VALUES,
                    LINK,TYPE,TYPEVALUE,TOTAL,EXTRA,EXECUTE,MAS,MENOS,MUL,DIV,PA_A,
                    PA_C,CO_A,CO_C,LLA_A,LLA_C,COMA,DOSPUNTOS,PUNTOCOMA,STRING,NUMBERS,
                    DECIMAL;


/* Non terminals */
non terminal                s;
non terminal GraficaPie     contPie;
non terminal GraficaBarra   contBarra;
non terminal Pila<Double>   contEjeY;
non terminal Pila<String>   contEjeX;
non terminal Pila<Union>    contUnir;   
non terminal Double         e,t,f;


/* The grammar rules */

s   ::= s DEF:ini GRAPHICBARRA contBarra:gb LLA_C:fin   {:
                                                            if(gb!=null){
                                                                System.out.println("Grafica: "+gb.getTitulo());
                                                                if(!definition_error((Token)ini,(Token)fin,gb.verificarGrafica())){
                                                                    System.out.println("Grafica Valida");
                                                                }else{
                                                                    System.out.println("Grafica No Valida");
                                                                }
                                                            }
                                                        :}
    |   s DEF:ini GRAPHICPIE contPie:gp LLA_C:fin   {:
                                                        if(gp!=null){
                                                            System.out.println("Grafica: "+gp.getTitulo());
                                                            if(!definition_error((Token)ini,(Token)fin,gp.verificarGrafica())){
                                                                System.out.println("Grafica Valida");
                                                            }else{
                                                                System.out.println("Grafica No Valida");
                                                            }
                                                        }
                                                    :}
    |   s EXECUTE:ex PA_A STRING:gn PA_C PUNTOCOMA  {:
                                                        System.out.println("Grafico ejecutar: "+(String)((Token)gn).getValue());
                                                    :}
    |   DEF:ini GRAPHICBARRA contBarra:gb LLA_C:fin {:
                                                        if(gb!=null){
                                                            System.out.println("Grafica: "+gb.getTitulo());
                                                            if(!definition_error((Token)ini,(Token)fin,gb.verificarGrafica())){
                                                                System.out.println("Grafica Valida");
                                                            }else{
                                                                System.out.println("Grafica No Valida");
                                                            }
                                                        }
                                                    :}
    |   DEF:ini GRAPHICPIE contPie:gp LLA_C:fin {:
                                                    if(gp!=null){
                                                        System.out.println("Grafica: "+gp.getTitulo());
                                                        if(!definition_error((Token)ini,(Token)fin,gp.verificarGrafica())){
                                                            System.out.println("Grafica Valida");
                                                        }else{
                                                            System.out.println("Grafica No Valida");
                                                        }
                                                    }
                                                :}
    ;

//CONTENIDO DE UNA GRAFICA DE BARRAS

contBarra   ::= contBarra:gb TITTLE:e DOSPUNTOS STRING:titulo PUNTOCOMA {:
                                                                            if(gb !=null){
                                                                                RESULT = gb;
                                                                                if(RESULT.getTitulo()!=null){
                                                                                    semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                }else{
                                                                                    String tittle = (String)((Token)titulo).getValue();
                                                                                    RESULT.setTitulo(tittle);
                                                                                }
                                                                            }
                                                                        :}
            |   contBarra:gb EJEX:e DOSPUNTOS CO_A contEjeX:pila PUNTOCOMA  {:
                                                                                if(gb !=null){
                                                                                    RESULT = gb;
                                                                                    if(RESULT.getEjex()!=null){
                                                                                        semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                    }else{
                                                                                        if(pila!=null){
                                                                                            RESULT.setEjex(pila.toArray(String[].class));
                                                                                        }
                                                                                    }
                                                                                }
                                                                            :}
            |   contBarra:gb EJEY:e DOSPUNTOS CO_A contEjeY:pila PUNTOCOMA  {:
                                                                                if(gb !=null){
                                                                                    RESULT = gb;
                                                                                    if(RESULT.getEjey()!=null){
                                                                                        semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                    }else{
                                                                                        if(pila!=null){
                                                                                            RESULT.setEjey(pila.toArray(Double[].class));
                                                                                        }
                                                                                    }
                                                                                }
                                                                            :}
            |   contBarra:gb LINK:e DOSPUNTOS CO_A contUnir:pila PUNTOCOMA  {:
                                                                                
                                                                                if(gb !=null){
                                                                                    RESULT = gb;
                                                                                    if(RESULT.getUnir()!=null){
                                                                                        semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                    }else{
                                                                                        if(pila!=null){
                                                                                            RESULT.setUnir(pila.toArrayList());
                                                                                        }
                                                                                    }
                                                                                }
                                                                            :}
            |   LLA_A                                                       {:
                                                                                RESULT = new GraficaBarra();
                                                                            :}
            |   contBarra error
            ;

contPie     ::= contPie:gp TITTLE:e DOSPUNTOS STRING:titulo PUNTOCOMA   {:
                                                                            if(gp!=null){
                                                                                RESULT = gp;
                                                                                if(RESULT.getTitulo()!=null){
                                                                                    semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                }else{
                                                                                    RESULT.setTitulo((String)((Token)titulo).getValue());
                                                                                }
                                                                            }
                                                                        :}
            |   contPie:gp TYPE:e DOSPUNTOS TYPEVALUE:tipo PUNTOCOMA    {:
                                                                            if(gp!=null){
                                                                                RESULT = gp;
                                                                                if(RESULT.getTipo()!=null){
                                                                                    semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                }else{
                                                                                    RESULT.setTipo((String)((Token)tipo).getValue());
                                                                                }
                                                                                
                                                                            }
                                                                        :}
            |   contPie:gp LABEL:e DOSPUNTOS CO_A contEjeX:pila PUNTOCOMA   {:
                                                                                
                                                                                if(gp!=null){
                                                                                    RESULT = gp;
                                                                                    if(RESULT.getEtiquetas()!=null){
                                                                                        semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                    }else{
                                                                                        if(pila!=null){
                                                                                            RESULT.setEtiquetas(pila.toArray(String[].class));
                                                                                        }
                                                                                    }
                                                                                }
                                                                            :}
            |   contPie:gp VALUES:e DOSPUNTOS CO_A contEjeY:pila PUNTOCOMA  {:
                                                                                
                                                                                if(gp!=null){
                                                                                    RESULT = gp;
                                                                                    if(RESULT.getValores()!=null){
                                                                                        semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                    }else{
                                                                                        if(pila!=null){
                                                                                            RESULT.setValores(pila.toArray(Double[].class));
                                                                                        }
                                                                                    }
                                                                                }
                                                                            :} 
            |   contPie:gp TOTAL:e DOSPUNTOS e:num PUNTOCOMA    {:
                                                                    if(gp!=null){
                                                                        RESULT = gp;
                                                                        if(RESULT.getTotal()!=null){
                                                                            semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                        }else{
                                                                            if(num!=null){
                                                                                RESULT.setTotal(num);
                                                                            }
                                                                        }
                                                                    }
                                                                :}
            |   contPie:gp LINK:e DOSPUNTOS CO_A contUnir:pila PUNTOCOMA    {:
                                                                                
                                                                                if(gp!=null){
                                                                                    RESULT = gp;
                                                                                    if(RESULT.getUnir()!=null){
                                                                                        semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                                    }else{
                                                                                        if(pila!=null){
                                                                                            RESULT.setUnir(pila.toArrayList());
                                                                                        }
                                                                                    }
                                                                                }
                                                                                
                                                                            :}
            |   contPie:gp EXTRA:e DOSPUNTOS STRING:extra PUNTOCOMA   {:
                                                                        if(gp!=null){
                                                                            RESULT = gp;
                                                                            if(RESULT.getExtra()!=null){
                                                                                semantic_error(((Token)e),"La propiedad ya habia sido definida en el grafico");
                                                                            }else{
                                                                                RESULT.setExtra((String)((Token)extra).getValue());
                                                                            }
                                                                        }
                                                                    :}
            |   LLA_A   {:
                            RESULT = new GraficaPie();
                        :}
            |   contPie error
            ;


//GRAMAICA PARA EL RECONOCIMIENTO DE NUMEROS ENTEROS Y DECIMALES
e   ::= e:num1 MAS t:num2   {:
                                RESULT = (num1 + num2);
                            :}
    |   e:num1 MENOS t:num2 {:
                                RESULT = (num1 - num2);
                            :}
    |   t:exp               {:
                                RESULT = exp;
                            :}
    ;

t   ::= t:num1 MUL f:num2   {:
                                RESULT = (num1*num2);
                            :}
    |   t:num1 DIV f:num2   {:
                                RESULT = (num1/num2);
                            :}             
    |   f:exp               {:
                                RESULT = exp;
                            :}
    ;

f   ::= NUMBERS:num     {:
                            RESULT = (Double) ((Token)num).getValue();
                        :}
    |   DECIMAL:num     {:
                            RESULT = (Double) ((Token)num).getValue();
                        :}
    |   PA_A e:exp PA_C
                        {:
                            RESULT = exp;
                        :}
    |   error f
    ;
// recuperacion de vectores del lenguaje
contEjeY    ::= e:num1 COMA contEjeY:pila   {:
                                                RESULT = pila;
                                                if(RESULT != null){
                                                    RESULT.push(num1);
                                                }
                                            :}
            |   e:num1 CO_C {:
                                RESULT = new Pila<>();
                                RESULT.push(num1);
                            :}
            ;

contEjeX    ::= STRING:cadena COMA contEjeX:pila    {:
                                                        RESULT = pila;
                                                        if(RESULT!=null){
                                                            String strin = (String)((Token)cadena).getValue();
                                                            RESULT.push(strin);                    
                                                        }
                                                    :}
            |   STRING:cadena CO_C  {:
                                        RESULT = new Pila<>();
                                        String strin = (String)((Token)cadena).getValue();
                                        RESULT.push(strin);
                                    :}
            ;

contUnir    ::= LLA_A e:num1 COMA e:num2 LLA_C COMA contUnir:pila   {:
                                                                        RESULT = pila;
                                                                        if(RESULT!=null){
                                                                            if(num1!=null && num2!=null){
                                                                                Double mod1 = num1 % 2;
                                                                                Double mod2 = num2 % 2;
                                                                                if((mod1==1||mod1==0)&&(mod2==1||mod2==0)){
                                                                                    System.out.println("El numero: "+num1+", es entero");
                                                                                }else{
                                                                                    System.out.println("El numero: "+num1+", es decimal");
                                                                                }
                                                                                RESULT.push(new Union(num1.intValue(),num2.intValue()));
                                                                            }
                                                                        }
                                                                    :}
            |   LLA_A e:num1 COMA e:num2 LLA_C CO_C                 {:
                                                                        
                                                                        if(num1!=null && num2!=null){
                                                                            RESULT = new Pila<>();
                                                                            Double mod1 = num1 % 2;
                                                                            Double mod2 = num2 % 2;
                                                                            if((mod1==1||mod1==0)&&(mod2==1||mod2==0)){
                                                                                System.out.println("El numero: "+num1+", es entero");
                                                                            }else{
                                                                                System.out.println("El numero: "+num1+", es decimal");
                                                                            }
                                                                            RESULT.push(new Union(num1.intValue(),num2.intValue()));
                                                                        }
                                                                    :}
            ;